#!/usr/bin/perl

use strict;
use warnings;

my $conn = shift or die "usage: $0 #-CONN\n";

my @points;

while (<>) {
    chomp;
    my @p = split /,/;
    push @points, { C => \@p, K => key(\@p) };
}

my %conn;
my %cache;

while ($conn > 0) {
    my($min, $end1, $end2);
    for my $p1 (@points) {
        for my $p2 (@points) {
            next if $p1->{K} eq $p2->{K};
            my($sp1, $sp2) = compare($p1, $p2) < 0 ? ($p1, $p2) : ($p2, $p1);
            my $dist = dist($sp1, $sp2);
            if (!defined $min || $dist < $min) {
                if (!exists $conn{$sp1->{K}}{$sp2->{K}}) {
                    $min = $dist;
                    $end1 = $sp1;
                    $end2 = $sp2;
                }
            }
        }
    }
    print "Connecting $end1->{K} to $end2->{K}.\n";
    ++$conn{$end1->{K}}{$end2->{K}};
    ++$conn{$end2->{K}}{$end1->{K}};
    --$conn;
}

for my $p1 (sort keys %conn) {
    for my $p2 (sort keys %{ $conn{$p1} }) {
        print "$p1 is connected to $p2\n";
    }
}

my %circ_seen;
my @sizes;

for my $p (sort keys %conn) {
    next if $circ_seen{$p}++;
    my %seen = ($p => 1);
    my @queue = sort keys %{ $conn{$p} };
    while (@queue) {
        my $c = shift @queue;
        $circ_seen{$c}++;
        next if $seen{$c}++;
        push @queue, sort keys %{ $conn{$c} };
    }
    print "$p is in a circuit with ", join(", ", sort keys %seen), "\n";
    my $size = keys %seen;
    print "Found circuit of size $size starting at $p.\n";
    push @sizes, $size;
}

@sizes = sort { $b <=> $a } @sizes;

my $prod = $sizes[0] * $sizes[1] * $sizes[2];

print "\nproduct = $prod\n\n";

sub key {
    my($p) = @_;
    return join(",", @{ $p });
}

sub compare {
    my($p1, $p2) = @_;

    my($c1, $c2) = ($p1->{C}, $p2->{C});
    
    return $c1->[0] <=> $c2->[0] || $c1->[1] <=> $c2->[1] || $c1->[2] <=> $c2->[2];
}

sub dist {
    my($p1, $p2) = @_;

    ($p1, $p2) = ($p2, $p1) if compare($p1, $p2) > 0;

    my $cache_key = $p1->{K} . " to " . $p2->{K};

    return $cache{$cache_key} if exists $cache{$cache_key};

    my($c1, $c2) = ($p1->{C}, $p2->{C});
    
    my $dist = sqrt( ($c1->[0] - $c2->[0]) * ($c1->[0] - $c2->[0]) +
                     ($c1->[1] - $c2->[1]) * ($c1->[1] - $c2->[1]) +
                     ($c1->[2] - $c2->[2]) * ($c1->[2] - $c2->[2]) );

    $cache{$cache_key} = $dist;

    return $dist;
}
    
